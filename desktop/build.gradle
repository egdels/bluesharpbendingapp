/*
 * Copyright (c) 2023 Christian Kierdorf
 *
 * Permission is hereby granted, free of charge, to any person obtaining a
 * copy of this software and associated documentation files (the “Software”),
 * to deal in the Software without restriction, including without limitation
 * the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or
 * sell copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED “AS IS”, WITHOUT WARRANTY OF ANY KIND,
 * EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
 * OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
 * HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
 * WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
 * FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
 * OTHER DEALINGS IN THE SOFTWARE.
 *
 */
plugins {
    // Enables the Java plugin for basic configuration of Java projects
    id 'java'
    // Adds the 'org.beryx.runtime' plugin for creating runtime packages (e.g., jpackage for user distributions)
    id 'org.beryx.runtime' version '1.12.7'
    id 'org.openjfx.javafxplugin' version '0.0.13'  // JavaFX Plugin
}

javafx {
    version = '21.0.6'  // Wählen Sie die passende Version
    modules = [
            'javafx.controls',
            'javafx.fxml',
            'javafx.graphics'
    ]
}

dependencies {
    // Adds dependency on the 'base' module
    implementation project(':base')
    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'
}

application {
    // Defines the main class of the application, containing the `main()` method
    mainClass = 'de.schliweb.bluesharpbendingapp.app.MainDesktop'
    // Sets the name of the application
    applicationName = 'BluesHarpBendingApp'
}

jar {
    manifest {
        // Adds a version entry in the `MANIFEST.MF` file, which can later be accessed (e.g., using getImplementationVersion())
        attributes(
                'Implementation-Version': version
        )
    }
}

runtime {
    modules = [
            'javafx.controls',
            'javafx.fxml',
            'javafx.graphics'
    ]
    // Configures runtime environment optimizations
    options = [
            '--strip-debug', // Removes debugging information to reduce runtime size
            '--compress', '2', // Compresses the runtime image (level 2)
            '--no-header-files', // Removes header files that are not necessary for end users
            '--no-man-pages' // Removes manual pages
    ]

    launcher {
        // Sets up a launcher without console output
        noConsole = true
    }

    jpackage {
        // Defines platform-specific options for building installation packages
        def currentOs = org.gradle.internal.os.OperatingSystem.current() // Detects the current operating system

        // Determines the correct icon format based on the operating system
        def imgType = currentOs.windows ? 'ico' : currentOs.macOsX ? 'icns' : 'png'

        // Adds an application icon based on the operating system
        imageOptions += ['--icon', "src/main/resources/ic_launcher_round.$imgType"]

        imageName = 'bluesharpbendingapp'
        installerName = 'bluesharpbendingapp'

        // Includes additional resources from the "src/main/resources" directory
        installerOptions += ['--resource-dir', "src/main/resources"]

        // Defines the application's vendor/creator
        installerOptions += ['--vendor', 'letsbend.de']

        // Passes an argument to the application
        args = ['donationware']

        // Platform-specific installer options:
        if (currentOs.windows) {
            // For Windows: Enables per-user installation, directory chooser, and Start Menu/Desktop shortcuts
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
        } else if (currentOs.linux) {
            // For Linux: Creates a `.deb` package with proper settings, including shortcuts
            installerOptions += ['--type', 'deb', '--linux-package-name', 'bluesharpbendingapp', '--linux-app-release', version ,'--linux-deb-maintainer', 'christian.kierdorf@schliweb.de', '--linux-shortcut']
        } else if (currentOs.macOsX) {
            // For macOS: Specifies the package name for Mac
            installerOptions += ['--mac-package-name', 'BluesharpBendingApp', '--mac-package-identifier', 'BluesharpBendingApp']
        }
    }
}

test {
    // Enables the use of JUnit 5 (JUnit Platform) for testing
    useJUnitPlatform()
}